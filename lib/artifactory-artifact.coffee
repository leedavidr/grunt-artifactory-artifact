
module.exports = (grunt) -> class ArtifactoryArtifact

	_ = require('lodash')


	# If an ID string is provided, this will return a config object suitable for creation of an ArtifactoryArtifact object
	# ID made from group_id:name:ext:{classifier:}version
	@fromString = (idString) ->
		config = {}
		parts = idString.split(':')
		if parts.length == 4
			parts.splice(3, 0, '')
		[config.group_id, config.name, config.ext, config.classifier, config.version] = parts
		return config

	constructor: (config) ->
		{@archive, @url, @base_path, @download_url, @repository, @group_id, @name, @classifier, @version, @ext, @versionPattern} = config

	toString: () ->
		parts = [@group_id, @name, @ext, @version]
		if @classifier
			parts.splice(3, 0, @classifier)
		parts.join(':')

	dashClassifier: () ->
		if @classifier then "-#{@classifier}" else ''

	buildUrlPath: () ->
		_.compact(_.flatten([
			@url
			@base_path
			@repository
			@group_id.split('.')
			@name
			"#{@version}/"
		])).join('/')

	buildUrl: () ->
		if @download_url
			@download_url
		else
			"#{@buildUrlPath()}#{@buildArtifactUri()}"

	buildArtifactUri: () ->
		if @archive
		then @archive
		else @versionPattern.replace /%([avce])/g, ($0, $1) =>
			{a: @name, v: @version, c: @dashClassifier(), e: @ext}[$1]
